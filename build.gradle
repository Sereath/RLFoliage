buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://maven.gofancy.wtf/releases'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.40"
        classpath group: 'wtf.gofancy.fancygradle', name: 'wtf.gofancy.fancygradle.gradle.plugin', version: '1.1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'wtf.gofancy.fancygradle'

version = "${version}"
group = "${group}"
archivesBaseName = jarName

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

fancyGradle {
    patches {
        resources
        coremods
    }
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/BetterFoliage_at.cfg')
}

repositories {
    mavenCentral()
    maven {
        url = "https://www.cursemaven.com"
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("curse.maven:shadowfacts-forgelin-248453:2785465")
}

processResources {
    inputs.property "version", version
    inputs.property "mcversion", mc_version

    filesMatching('mcmod.info') {
        expand 'version': version, 'mcversion': mc_version
    }
    //into "${buildDir}/classes/main"
}

jar {
    manifest {
        attributes "FMLCorePlugin": "mods.betterfoliage.loader.BetterFoliageLoader"
        attributes "FMLCorePluginContainsFMLMod": "mods.betterfoliage.BetterFoliageMod"
        attributes "FMLAT": "BetterFoliage_at.cfg"
    }
    exclude "optifine"
}